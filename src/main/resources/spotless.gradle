printf("Configuring spotless\n")

// Spotless autoformatter
// See https://github.com/diffplug/spotless/tree/main/plugin-gradle
// Can be locally toggled via spotless:off/spotless:on comments
spotless {
    encoding 'UTF-8'

    format 'misc', {
        target '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        target 'src/*/java/**/*.java'

        toggleOffOn()
        importOrder()
        removeUnusedImports()
        if (enableModernJavaSyntax.toBoolean()) {
            palantirJavaFormat('2.28.0')
        } else {
            palantirJavaFormat('1.1.0') // last version supporting jvm 8
        }
    }
    kotlin {
        target 'src/*/kotlin/**/*.kt'

        toggleOffOn()
        ktfmt('0.39')

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}
